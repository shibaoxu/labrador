buildscript {
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies{
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.21"
        classpath "io.franzbecker:gradle-lombok:1.14"

    }
}

ext{
    javaApps = [
            ':authservice',':adminservice', 'accountservice',
            ':commons', ':commons-test',
            ':eurekaservice', ':configservice', ':turbineservice', ':zuulservice'
    ]
    bootApps = javaApps - ':commons' - ':commons-test'
    cloudServer = [':eurekaservice', ':configservice', ':turbineservice', ':zuulservice']
    serviceApps = bootApps - cloudServer
    jsApps = []
}

allprojects {
    group = 'com.labrador'
    version = '0.0.1-SNAPSHOT'
}

apply from: "gradle-configuration/configJavaApps.gradle"
apply from: "gradle-configuration/configCheck.gradle"
apply from: "gradle-configuration/configBootApps.gradle"
apply from: "gradle-configuration/configServiceApps.gradle"